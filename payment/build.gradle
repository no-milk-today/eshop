plugins {
    id 'org.springframework.boot' version '3.4.4'
    id "org.openapi.generator" version "7.12.0"
}

springBoot {
    mainClass = 'com.yandex.reactive.testcontainers.payment.OpenApiGeneratorApplication'
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/api-spec.yaml") // projectDir - корень подпроекта payment
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("com.yandex.reactive.testcontainers.payment.model")
    apiPackage.set("com.yandex.reactive.testcontainers.payment.api")
    configOptions.set([
            openApiNullable: "false",
            interfaceOnly  : "false",
            library        : "spring-boot",
            reactive       : "true",              // указывает генератору дать reactive контроллеры
            useTags        : "true",
            useSpringBoot3 : "true"             // https://stackoverflow.com/questions/74593513/is-there-a-way-to-configure-openapi-generator-to-use-jakarta-package-during-gene
    ])
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation' // валидацию для DTO
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.5'
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}
