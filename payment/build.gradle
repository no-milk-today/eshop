plugins {
    id 'org.springframework.boot' version '3.4.4'
    id "org.openapi.generator" version "7.12.0"
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/api-spec.yaml") // projectDir - корень подпроекта payment
    outputDir.set("$buildDir/generated")
    modelPackage.set("com.yandex.reactive.testcontainers.payment.model")
    apiPackage.set("com.yandex.reactive.testcontainers.payment.api")
    configOptions.set([
            interfaceOnly: "false",
            library      : "spring-boot",
            reactive     : "true",              // указывает генератору дать reactive контроллеры
            useTags      : "true",
            useSpringBoot3 : "true"             // https://stackoverflow.com/questions/74593513/is-there-a-way-to-configure-openapi-generator-to-use-jakarta-package-during-gene
    ])
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation' // валидацию для DTO
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
}
